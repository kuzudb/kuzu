-NAME BI14
-QUERY MATCH
         (country1:Place {name: 'Chile'})<-[:isPartOf]-(city1:Place)<-[:personIsLocatedIn]-(person1:Person),
         (country2:Place {name: 'Argentina'})<-[:isPartOf]-(city2:Place)<-[:personIsLocatedIn]-(person2:Person),
         (person1)-[:KNOWS]-(person2)
       WHERE city1.type='city' AND city2.type='city'
       WITH person1, person2, city1, 0 AS score
       OPTIONAL MATCH (person1)<-[:messageHasCreator]-(c:Comment)-[:replyOf]->(:Comment:Post)-[:messageHasCreator]->(person2)
       WITH DISTINCT person1, person2, city1, score + (CASE WHEN c IS NULL THEN 0 ELSE  4 END) AS score
       OPTIONAL MATCH (person1)<-[:messageHasCreator]-(m:Comment:Post)<-[:replyOf]-(:Comment)-[:messageHasCreator]->(person2)
       WITH DISTINCT person1, person2, city1, score + (CASE WHEN m IS NULL THEN 0 ELSE  1 END) AS score
       OPTIONAL MATCH (person1)-[:likeMessage]->(m:Comment:Post)-[:messageHasCreator]->(person2)
       WITH DISTINCT person1, person2, city1, score + (CASE WHEN m IS NULL THEN 0 ELSE 10 END) AS score
       OPTIONAL MATCH (person1)<-[:messageHasCreator]-(m:Comment:Post)<-[:likeMessage]-(person2)
       WITH DISTINCT person1, person2, city1, score + (CASE WHEN m IS NULL THEN 0 ELSE  1 END) AS score
       ORDER BY
         city1.name ASC,
         score DESC,
         person1.id ASC,
         person2.id ASC
       WITH city1, collect({score: score, person1Id: person1.id, person2Id: person2.id})[0] AS top
       RETURN
         top.person1Id,
         top.person2Id,
         city1.name,
         top.score
       ORDER BY
         top.score DESC,
         top.person1Id ASC,
         top.person2Id ASC
       LIMIT 100
---- 1
