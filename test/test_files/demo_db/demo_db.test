-GROUP DemoDBTest
-DATASET CSV demo-db/csv

--

-CASE DemoDBTest

-DEBUG_LOG Limit1
-QUERY MATCH (u:User) RETURN u.name ORDER BY u.age DESC LIMIT 3;
---- 3
Zhang
Karissa
Adam

-DEBUG_LOG MatchSingleNodeLabel
-QUERY MATCH (a:User) RETURN a;
---- 4
(label:User, 0:0, {name:Adam, age:30})
(label:User, 0:1, {name:Karissa, age:40})
(label:User, 0:2, {name:Zhang, age:50})
(label:User, 0:3, {name:Noura, age:25})

-DEBUG_LOG MatchMultipleNodeLabels
-QUERY MATCH (a:User:City) RETURN a;
---- 7
(label:City, 1:0, {name:Waterloo, age:, population:150000})
(label:City, 1:1, {name:Kitchener, age:, population:200000})
(label:City, 1:2, {name:Guelph, age:, population:75000})
(label:User, 0:0, {name:Adam, age:30, population:})
(label:User, 0:1, {name:Karissa, age:40, population:})
(label:User, 0:2, {name:Zhang, age:50, population:})
(label:User, 0:3, {name:Noura, age:25, population:})

-DEBUG_LOG MatchAnyNodeLabel
-QUERY MATCH (a) RETURN a;
---- 7
(label:City, 1:0, {name:Waterloo, age:, population:150000})
(label:City, 1:1, {name:Kitchener, age:, population:200000})
(label:City, 1:2, {name:Guelph, age:, population:75000})
(label:User, 0:0, {name:Adam, age:30, population:})
(label:User, 0:1, {name:Karissa, age:40, population:})
(label:User, 0:2, {name:Zhang, age:50, population:})
(label:User, 0:3, {name:Noura, age:25, population:})

-DEBUG_LOG MatchSingleRelLabel
-QUERY MATCH (a:User)-[e:Follows]->(b:User) RETURN a.name, e, b.name;
---- 4
Adam|(0:0)-[label:Follows, {_id:2:0, since:2020}]->(0:1)|Karissa
Adam|(0:0)-[label:Follows, {_id:2:1, since:2020}]->(0:2)|Zhang
Karissa|(0:1)-[label:Follows, {_id:2:2, since:2021}]->(0:2)|Zhang
Zhang|(0:2)-[label:Follows, {_id:2:3, since:2022}]->(0:3)|Noura

-DEBUG_LOG MatchMultipleRelLabels
-QUERY MATCH (a:User)-[e:Follows|:LivesIn]->(b:User:City) RETURN a.name, e, b.name;
---- 8
Adam|(0:0)-[label:Follows, {_id:2:0, since:2020}]->(0:1)|Karissa
Adam|(0:0)-[label:Follows, {_id:2:1, since:2020}]->(0:2)|Zhang
Adam|(0:0)-[label:LivesIn, {_id:3:0, since:}]->(1:0)|Waterloo
Karissa|(0:1)-[label:Follows, {_id:2:2, since:2021}]->(0:2)|Zhang
Karissa|(0:1)-[label:LivesIn, {_id:3:1, since:}]->(1:0)|Waterloo
Noura|(0:3)-[label:LivesIn, {_id:3:3, since:}]->(1:2)|Guelph
Zhang|(0:2)-[label:Follows, {_id:2:3, since:2022}]->(0:3)|Noura
Zhang|(0:2)-[label:LivesIn, {_id:3:2, since:}]->(1:1)|Kitchener

-DEBUG_LOG MatchAnyRelLabel
-QUERY MATCH ()-[e]->() RETURN e;
---- 8
(0:0)-[label:Follows, {_id:2:0, since:2020}]->(0:1)
(0:0)-[label:Follows, {_id:2:1, since:2020}]->(0:2)
(0:0)-[label:LivesIn, {_id:3:0, since:}]->(1:0)
(0:1)-[label:Follows, {_id:2:2, since:2021}]->(0:2)
(0:1)-[label:LivesIn, {_id:3:1, since:}]->(1:0)
(0:2)-[label:Follows, {_id:2:3, since:2022}]->(0:3)
(0:2)-[label:LivesIn, {_id:3:2, since:}]->(1:1)
(0:3)-[label:LivesIn, {_id:3:3, since:}]->(1:2)

-DEBUG_LOG MatchUndirected
-QUERY MATCH (a:User)-[e:Follows]-(b:User) WHERE a.name = 'Karissa' RETURN b.name;
---- 2
Adam
Zhang

-DEBUG_LOG MatchTwoHop
-QUERY MATCH (a:User)-[:Follows]->(:User)-[:LivesIn]->(c:City) WHERE a.name = "Adam" RETURN a, c.name, c.population;
---- 2
(label:User, 0:0, {name:Adam, age:30})|Kitchener|200000
(label:User, 0:0, {name:Adam, age:30})|Waterloo|150000

-DEBUG_LOG MatchCyclic
-QUERY MATCH (a:User)-[:Follows]->(b:User)-[:Follows]->(c:User), (a)-[:Follows]->(c) RETURN a.name, b.name, c.name;
---- 1
Adam|Karissa|Zhang

-DEBUG_LOG MatchFilter
-QUERY MATCH (a:User)-[e:Follows {since: 2020}]->(b:User {name: "Zhang"}) RETURN a, e.since, b.name;
---- 1
(label:User, 0:0, {name:Adam, age:30})|2020|Zhang

-DEBUG_LOG MatchVarLen
-QUERY MATCH (a:User)-[e:Follows*1..2]->(b:User) WHERE a.name = 'Adam' RETURN b.name, length(e) AS length;
---- 4
Karissa|1
Zhang|2
Zhang|1
Noura|2

-DEBUG_LOG MatchUndirectedVarLen
-QUERY MATCH (a:User)-[e*2..2]-(b) WHERE a.name = 'Noura' AND b.name <> 'Noura' RETURN b.name, length(e) AS length;
---- 3
Adam|2
Karissa|2
Kitchener|2

-DEBUG_LOG ReturnVarLen
-QUERY MATCH (a:User)-[e:Follows*1..2]->(b:User) WHERE a.name = 'Adam' RETURN b.name, e;
---- 4
Karissa|{_NODES: [{_ID: 0:0, NAME: Adam, AGE: 30},{_ID: 0:1, NAME: Karissa, AGE: 40}], _RELS: [{_ID: 2:0, SINCE: 2020}]}
Noura|{_NODES: [{_ID: 0:0, NAME: Adam, AGE: 30},{_ID: 0:2, NAME: Zhang, AGE: 50},{_ID: 0:3, NAME: Noura, AGE: 25}], _RELS: [{_ID: 2:1, SINCE: 2020},{_ID: 2:3, SINCE: 2022}]}
Zhang|{_NODES: [{_ID: 0:0, NAME: Adam, AGE: 30},{_ID: 0:1, NAME: Karissa, AGE: 40},{_ID: 0:2, NAME: Zhang, AGE: 50}], _RELS: [{_ID: 2:0, SINCE: 2020},{_ID: 2:2, SINCE: 2021}]}
Zhang|{_NODES: [{_ID: 0:0, NAME: Adam, AGE: 30},{_ID: 0:2, NAME: Zhang, AGE: 50}], _RELS: [{_ID: 2:1, SINCE: 2020}]}

-DEBUG_LOG ShortestPath
-QUERY MATCH (a:User)-[e* SHORTEST 1..4]->(b:City) WHERE a.name = 'Adam' RETURN b.name, length(e) AS length;
-PARALLELISM 1
---- 3
Guelph|3
Kitchener|2
Waterloo|1


-DEBUG_LOG OptionalMatch1
-QUERY MATCH (u:User) OPTIONAL MATCH (u)-[:Follows]->(u1:User) RETURN u.name, u1.name;
---- 5
Adam|Karissa
Adam|Zhang
Karissa|Zhang
Zhang|Noura
Noura|

-DEBUG_LOG Return1
-QUERY MATCH (a:User)-[e:Follows]->(b:User) RETURN a, e;
---- 4
(label:User, 0:0, {name:Adam, age:30})|(0:0)-[label:Follows, {_id:2:0, since:2020}]->(0:1)
(label:User, 0:0, {name:Adam, age:30})|(0:0)-[label:Follows, {_id:2:1, since:2020}]->(0:2)
(label:User, 0:1, {name:Karissa, age:40})|(0:1)-[label:Follows, {_id:2:2, since:2021}]->(0:2)
(label:User, 0:2, {name:Zhang, age:50})|(0:2)-[label:Follows, {_id:2:3, since:2022}]->(0:3)

-DEBUG_LOG Return2
-QUERY MATCH (a:User)-[:Follows]->(b:User) RETURN *;
---- 4
(label:User, 0:0, {name:Adam, age:30})|(label:User, 0:1, {name:Karissa, age:40})
(label:User, 0:0, {name:Adam, age:30})|(label:User, 0:2, {name:Zhang, age:50})
(label:User, 0:1, {name:Karissa, age:40})|(label:User, 0:2, {name:Zhang, age:50})
(label:User, 0:2, {name:Zhang, age:50})|(label:User, 0:3, {name:Noura, age:25})

-DEBUG_LOG Return3
-QUERY MATCH (a:User)-[e:Follows]->(b:User) RETURN a.name, a.age, e.since;
---- 4
Adam|30|2020
Adam|30|2020
Karissa|40|2021
Zhang|50|2022

-DEBUG_LOG ReturnDistinct
-QUERY MATCH (a:User)-[e:Follows]->(b:User) RETURN DISTINCT a.name, a.age, e.since;
---- 3
Adam|30|2020
Karissa|40|2021
Zhang|50|2022

-DEBUG_LOG ReturnGroupByAgg
-QUERY MATCH (a:User)-[e:Follows]->(b:User) RETURN a, avg(b.age) as avgFriendAge;
---- 3
(label:User, 0:0, {name:Adam, age:30})|45.000000
(label:User, 0:1, {name:Karissa, age:40})|50.000000
(label:User, 0:2, {name:Zhang, age:50})|25.000000

-DEBUG_LOG ReturnGroupByAgg2
-QUERY MATCH (u:User)-[:LivesIn]->(c:City) RETURN c.name, COUNT(*);
---- 3
Guelph|1
Kitchener|1
Waterloo|2

-DEBUG_LOG Skip1
-QUERY MATCH (u:User) RETURN u.name ORDER BY u.age SKIP 2;
---- 2
Karissa
Zhang

-DEBUG_LOG Union1
-QUERY MATCH (u1:User)-[:LivesIn]->(c1:City) WHERE c1.name = "Waterloo" RETURN u1.name UNION ALL MATCH (u2:User)-[:LivesIn]->(c2:City) WHERE c2.name = "Kitchener" RETURN u2.name;
---- 3
Karissa
Adam
Zhang

-DEBUG_LOG Union2
-QUERY MATCH (u1:User)-[:Follows]->(u2:User) WHERE u2.name = 'Zhang' RETURN u1.age UNION ALL MATCH (u3:User)-[:Follows]->(u4:User) WHERE u4.name = 'Karissa' RETURN u3.age;
---- 3
30
40
30

-DEBUG_LOG Union3
-QUERY MATCH (u1:User)-[:Follows]->(u2:User) WHERE u2.name = 'Zhang' RETURN u1.age UNION MATCH (u3:User)-[:Follows]->(u4:User) WHERE u4.name = 'Karissa' RETURN u3.age;
---- 2
30
40

-DEBUG_LOG Unwind1
-QUERY UNWIND ["Amy", "Bob", "Carol"] AS x RETURN 'name' as name, x;
---- 3
name|Amy
name|Bob
name|Carol

-DEBUG_LOG Unwind2
-QUERY UNWIND [["Amy"], ["Bob", "Carol"]] AS x RETURN x;
---- 2
[Amy]
[Bob,Carol]

-DEBUG_LOG Where1
-QUERY MATCH (a:User) WHERE a.age > 45 OR starts_with(a.name, "Kar") RETURN *;
---- 2
(label:User, 0:1, {name:Karissa, age:40})
(label:User, 0:2, {name:Zhang, age:50})

-DEBUG_LOG Where2
-QUERY MATCH (a:User) WHERE a.age IS NOT NULL AND starts_with(a.name, "Kar") RETURN *;
---- 1
(label:User, 0:1, {name:Karissa, age:40})

-DEBUG_LOG WhereExists1
-QUERY MATCH (a:User) WHERE a.age < 100 AND EXISTS { MATCH (a)-[:Follows*3..3]->(b:User)} RETURN a.name, a.age;
---- 1
Adam|30

-DEBUG_LOG WhereExists2
-QUERY MATCH (a:User) WHERE a.age < 100 AND EXISTS { MATCH (a)-[:Follows*3..3]->(b:User) WHERE EXISTS {MATCH (b)-[:Follows]->(c:User)} } RETURN a.name, a.age;
---- 0

-DEBUG_LOG WhereExists3
-QUERY MATCH (a:User) WHERE a.age < 100 AND EXISTS { MATCH (a)-[:Follows*3..3]->(b:User) WHERE EXISTS {MATCH (b)<-[:Follows]-(c:User)} } RETURN a.name, a.age;
---- 1
Adam|30

-DEBUG_LOG With1
-QUERY MATCH (a:User) WITH avg(a.age) as avgAge MATCH (b:User) WHERE b.age > avgAge RETURN *;
---- 2
36.250000|(label:User, 0:1, {name:Karissa, age:40})
36.250000|(label:User, 0:2, {name:Zhang, age:50})

-DEBUG_LOG With2
-QUERY MATCH (a:User) WITH a ORDER BY a.age DESC LIMIT 1 MATCH (a)-[:Follows]->(b:User) RETURN *;
---- 1
(label:User, 0:2, {name:Zhang, age:50})|(label:User, 0:3, {name:Noura, age:25})

-DEBUG_LOG Undir1
-QUERY MATCH (a:User)-[:Follows]-(b:User) RETURN a.name, b.age;
---- 8
Adam|40
Adam|50
Karissa|50
Zhang|25
Karissa|30
Zhang|30
Zhang|40
Noura|50

-DEBUG_LOG Undir2
-QUERY MATCH (a:User)-[:LivesIn]-(c:City) RETURN a.name, c.name;
---- 8
Adam|Waterloo
Karissa|Waterloo
Zhang|Kitchener
Noura|Guelph
Waterloo|Karissa
Waterloo|Adam
Kitchener|Zhang
Guelph|Noura

-DEBUG_LOG Undir3
-QUERY MATCH ()-[]-() RETURN COUNT(*);
---- 1
16
